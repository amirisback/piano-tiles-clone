plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {

    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 0

    def appDomain = "com"
    def appDevConsole = "github"
    def appName = "mihaimaximfii"

    def projectApplicationId = "$appDomain.$appDevConsole.$appName"
    def projectVersionCode = (versionMajor * 100) + (versionMinor * 10) + (versionPatch * 1)
    def projectVersionName = "$versionMajor.$versionMinor.$versionPatch"

    // Declaration app name
    def nameApp = "Piano Tiles"
    def nameAppLower = nameApp.toLowerCase()

    // Declaration apk / aab name
    def nameAPK = nameAppLower.replace(" ", "-")


    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId projectApplicationId
        minSdkVersion 21
        targetSdkVersion 30
        versionCode projectVersionCode
        versionName projectVersionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        // Declaration apps name debug mode
        def debugAttribute = " Development"
        def nameAppDebug = nameApp + debugAttribute

        // Naming APK // AAB
        setProperty("archivesBaseName", nameAPK + versionName + "#" + versionCode + "-"
                + new Date().format('ddMMyy'))

        resConfigs "en", "id"

        // Inject app name for debug
        resValue "string", "app_name", nameAppDebug

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Inject app name for release
            resValue "string", "app_name", nameApp

        }
    }

    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    testImplementation 'junit:junit:4.13.2'
    implementation 'com.squareup.picasso:picasso:2.71828'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}